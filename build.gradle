buildscript {
  ext {
    springBootVersion = '2.4.3'
    springBootDataGeodeVersion = '1.4.3'
  }
  repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/snapshot" }
    maven { url "https://repo.spring.io/milestone" }
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
  }
}

allprojects {
  repositories {
    mavenCentral()
    jcenter()
    maven { url 'https://repo.spring.io/libs-release' }
    maven { url 'https://repo.spring.io/libs-milestone' }
    maven { url 'https://repo.spring.io/libs-snapshot' }
    maven { url 'https://oss.jfrog.org/artifactory/oss-snapshot-local' }
    maven { url 'https://mvnrepository.com/artifact/org.springframework.plugin/spring-plugin' }
  }
}

subprojects {
  apply plugin: 'java'
  sourceCompatibility = 1.8

  group = 'example'
  version = '0.0.1-SNAPSHOT'

  configurations {
    provided
    compile.extendsFrom provided
  }

  task copyDependencies(type: Copy) {
    into "$buildDir/dependancies"
    from(configurations.compile - configurations.provided)
  }

  jar {
    dependsOn copyDependencies
  }
}

project(':client') {
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'
  dependencies {
    compile "org.springframework.geode:spring-geode-starter:${springBootDataGeodeVersion}"
  }
  bootRun {
    if (project.hasProperty('args')) {
      args project.args.split(' ')
    }
  }
}

project(':server') {
  dependencies {
    provided 'org.apache.geode:geode-core:1.13.1'
    provided 'org.apache.geode:geode-logging:1.13.1'
    provided 'org.apache.geode:geode-membership:1.13.1'
    provided 'org.apache.geode:geode-serialization:1.13.1'
    provided 'org.apache.logging.log4j:log4j-api:2.13.1'
  }
}
